variable "org_name" {}
variable "api_token" {}
variable "base_url" {}
variable "demo_app_name" { default = "travelagency" }
variable "udp_subdomain" { default = "local" }

locals {
  app_domain = var.udp_subdomain != "local" ? format("%s.%s.unidemo.info") : format("%s.%s", var.udp_subdomain, var.demo_app_name)
}

terraform {
  required_providers {
    okta = {
      source  = "okta/okta"
      version = "~> 3.17"
    }
  }
}

provider "okta" {
  org_name  = var.org_name
  api_token = var.api_token
  base_url  = var.base_url
}

data "okta_group" "all" {
  name = "Everyone"
}

resource "okta_app_oauth" "travelagency" {
  label       = format("%s DEMO (Generated by UDP)", local.app_domain)
  type        = "web"
  grant_types = ["authorization_code"]
  redirect_uris = [
    "https://${local.app_domain}/authorization-code/callback",
    "http://localhost:8666/authorization-code/callback"
  ]
  post_logout_redirect_uris = [
    "http://localhost:8666/index"
  ]
  response_types = ["code"]
  consent_method = "TRUSTED"
  issuer_mode    = "ORG_URL"
  lifecycle {
    ignore_changes = [groups]
  }
}

resource "okta_app_group_assignment" "travelagency" {
  app_id   = okta_app_oauth.travelagency.id
  group_id = data.okta_group.all.id
}

resource "okta_trusted_origin" "travelagency" {
  name   = format("%s: 8666", local.app_domain)
  origin = "https://${local.app_domain}"
  scopes = ["REDIRECT", "CORS"]
}

resource "okta_auth_server" "travelagency" {
  name        = local.app_domain
  description = format("%s (Generated by UDP)", local.app_domain)
  audiences   = ["api://${local.app_domain}"]
}

resource "okta_auth_server_policy" "travelagency" {
  auth_server_id   = okta_auth_server.travelagency.id
  status           = "ACTIVE"
  name             = "standard"
  description      = "Generated by UDP"
  priority         = 1
  client_whitelist = [okta_app_oauth.travelagency.id]
}

resource "okta_auth_server_policy_rule" "travelagency" {
  auth_server_id       = okta_auth_server.travelagency.id
  policy_id            = okta_auth_server_policy.travelagency.id
  status               = "ACTIVE"
  name                 = "one_hour"
  priority             = 1
  group_whitelist      = [data.okta_group.all.id]
  grant_type_whitelist = ["authorization_code"]
  scope_whitelist      = ["*"]
}

# Create the .env file
resource "local_file" "dotenv" {
  content = templatefile("${path.module}/travelagency.dotenv.tpl", {
    client_id         = okta_app_oauth.travelagency.client_id,
    client_secret     = okta_app_oauth.travelagency.client_secret,
    domain            = format("%s.%s", var.org_name, var.base_url),
    auth_server_id    = okta_auth_server.travelagency.id,
    okta_app_oauth_id = okta_app_oauth.travelagency.id
  })

  filename = format("%s/%s.env", "${path.module}", var.demo_app_name)
}

# Output to the terminal
output "client_id" {
  value = okta_app_oauth.travelagency.client_id
}

output "client_secret" {
  value     = okta_app_oauth.travelagency.client_secret
  sensitive = true
}

output "domain" {
  value = "${var.org_name}.${var.base_url}"
}

output "auth_server_id" {
  value = okta_auth_server.travelagency.id
}

output "issuer" {
  value = okta_auth_server.travelagency.issuer
}

output "okta_app_oauth_id" {
  value = okta_app_oauth.travelagency.id
}
